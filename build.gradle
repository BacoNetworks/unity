plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.11"
    id "org.jetbrains.kotlin.kapt" version "1.3.11"
    id "com.github.johnrengelman.shadow" version "4.0.3"
}

group "de.randombyte"
version "2.3.2"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven {
        name = "nucleus"
        url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.11"
    kapt "org.spongepowered:spongeapi:7.2.0"
    shadow("com.github.randombyte-developer.kosp:kosp:v1.9.1") { transitive = false }
    compile "io.github.nucleuspowered:nucleus-api:1.2.2-S7.0"
}
sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}
jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.unity.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.unity.shaded.kosp"

    classifier = null // Remove '-all' suffix from output file name
}
build.dependsOn shadowJar


compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}